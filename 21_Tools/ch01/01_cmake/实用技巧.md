
## 使用子进程命令
- 示例
  ```cmake
    set(command ls -al)
    execute_process(COMMAND ${command})
  ```
- 注意，这是在执行 CMakeLists.txt 创建的进程中下的一个子进程中执行的，所以并不能通过
  该种方法为 CMakeLists.txt 进程设置运行时的环境变量，比如:
  ```cmake
    execute_process(COMMAND "export MyName=CMake")
    message("myname=" $ENV{MyName})
  ```
  输出如下:
  ```shell
    myname=
  ```

## 设置环境变量
- 有的时候，在 cmake 文件内部设置环境变量是很有必要的。比如在编译项目时如果需要用到环境
  变量 PKG_CONFIG_PATH 来查找某个库，但又不想污染系统环境下的 PKG_CONFIG_PATH 变量，这
  时就可以通过下列方法在 cmake 执行进程内设置该变量:
  ```cmake
    SET(ENV{PKG_CONFIG_PATH} "/usr/local/libcurl/lib/pkgconfig")
    message("PKG_CONFIG_PATH=" $ENV{PKG_CONFIG_PATH})
  ```
  输出如下:
  ```shell
    PKG_CONFIG_PATH=/usr/local/libcurl/lib/pkgconfig
  ```

## 交叉编译设置
- 通过读取不同的环境变量，来设置不同的编译链接库
  ```cmake
    string(REGEX MATCH "arm64" IS_ARM64 "$ENV{ARCH}")
    if (IS_ARM64)
        message("arm64 host")
    else()
        message("x86 host")
    endif()
  ```
  
## 生成动态库时添加版本号
- 版本号又包括软件版本号和库版本号
- 只添加软件版本号
  ```cmake
    set(soname asn1)
    add_library(${soname} SHARED ${SRCS})
    set_target_properties(${soname} PROPERTIES VERSION 0.9.29)
  ```
  这个时候会生成两个文件 libasn1.so, libasn1.so.0.9.29, 前者是链接文件，后者是真正的库文件。
- 添加动态库版本号和API版本号(这里为 3)
  ```cmake
    set(soname asn1)
    add_library(${soname} SHARED ${SRCS})
    set_target_properties(${soname} PROPERTIES VERSION 0.9.29 SOVERSION 3)
  ```
  这个时候会生成三个文件 libasn1.so, libasn1.so.3, libasn1.so.0.9.29, 其中只有第三个是真正的库，
  前两个是链接文件，关系如下:
  ```shell
    libasn1.so -> libasn1.so.3
    libasn1.so.3 -> libasn1.so.0.9.29
  ```

## 换个地方保存目标二进制
- 可以通过 set 指令重新定义 `EXECUTABLE_OUTPUT_PATH` 和 `LIBRARY_OUTPUT_PATH` 变量来指定最终的
  目标二进制文件的位置(可执行文件或库文件，不包括中间文件)。
  ```cmake
    set(EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin)
    set(LIBRARY_OUTPUT_PATH ${PROJECT_BINARY_DIR}/lib)
  ```

## Debug 编译或 Release 编译时，分别指定的选项(只作参考)
- 参考 restbed
  ```cmake
    if ( CMAKE_BUILD_TYPE MATCHES Debug )
        set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11 -g -O0 -Wall -Wextra -Weffc++ -pedantic -Wno-unknown-pragmas" )
    else ( )
        string( REPLACE "-O3" "" CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE}" )
        set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11 -O2 -Wall -Wextra -Weffc++ -pedantic -Wno-unknown-pragmas" )
    endif ( )
  ```
  
## 检查编译器版本
- 参考 restbed
  ```cmake
    if ( ${CMAKE_CXX_COMPILER_ID} STREQUAL GNU )
        if ( ${CMAKE_CXX_COMPILER_VERSION} VERSION_LESS 4.8 )
            message( FATAL_ERROR "\nGCC version < 4.9\nYour systems default compiler is GCC. This project makes use of c++11 features present only in versions of gcc >= 4.9. You can use a different compiler by re-running cmake with the command switch \"-D CMAKE_CXX_COMPILER=<compiler>\" " )
        else ( )
            set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-non-virtual-dtor" )
        endif ( )
    elseif( ${CMAKE_CXX_COMPILER_ID} STREQUAL Clang )
        if ( ${CMAKE_CXX_COMPILER_VERSION} VERSION_LESS 3.3 )
            message( FATAL_ERROR "\nClang version < 3.3\nYour systems default compiler is clang. This project makes use of c++11 features present only in versions of clang >= 3.3. You can use a different compiler by re-running cmake with the command switch \"-D CMAKE_CXX_COMPILER=<compiler>\" " )
        else ( )
            set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}" )
        endif ( )
    elseif( ${CMAKE_CXX_COMPILER_ID} STREQUAL MSVC )
        if ( ${CMAKE_CXX_COMPILER_VERSION} VERSION_LESS 19.00.23026.0 )
            message( WARNING "\nMSVC compiler version < 19.00.23026.0" )
        endif ( )
        set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /D_WIN32_WINNT=0x0601 /W4 /wd4068 /wd4702" )
    else ( )
        message( FATAL_ERROR "Compiler not supported.")
    endif ( )
  ```