
### 子程序的定义与调用

Perl 中的子程序，也就是用户自定义的函数。其示例如下:
```pl
    sub marine {
        $n += 1;    # 全局变量 $n
        print "Hello, sailor number $n\n";
    }
```

子程序的定义是全局的，所以你可以在子程序中使用任何全局变量。

你可以在任意表达式中使用子程序名(前面加上 & 号)来调用它。

[示例程序](t/03_sub_call.pl)


### 返回值

子程序被调用时可以是一个独立的表达式，也可以作为表达式的某个部分。在作为表达式的某个部分时，可能需要对子程序的返回值作进一步的处理。

Perl 中所有的子程序都有一个返回值。如果没有 `return` 语句，子程序会将最后一次运算结果(不管是什么)当作子程序的返回值返回。
```pl
    sub sum_of_fred_and_barney {
        print "Hey, you called the sum_of_fred_and_barney subroutine!\n";
        $fred + $barney;    # 这就是返回值
    }
```

如果最后一行是一个子程序调用，那么子程序将会返回该子程序调用的返回值。

> 返回值为列表

```pl
    sub return_array
    {
        return (1..9);
    }

    my @array = &return_array();
```


> 返回值为哈希

```pl
    sub return_hash
    {
        return ("key1" => 100, "key2" => 200);
    }

    my %hash = &return_hash();
```


### 参数

Perl 以列表的形式传递参数:
```pl
    $n = &max(10, 15);
```
**Perl 会自动将参数列表化名为特殊的数组变量 @_，该变量在子程序执行期间有效。**

子程序可以通过访问 `@_` 数组来判断参数的个数及参数的值。
```pl
    sub max {
        if ($_[0] > $_[1]) {
            $_[0];
        } else {
            $_[1];
        }
    }
```

`@_` 变量是子程序的私有变量。如果好事者事先定义了同名的全局变量，则全局变量在子程序调用前会先被保存起来，并在子程序返回时恢复原本的值。

> 传递参数为列表

```pl
    sub print_array
    {
        foreach (@_) {
            print $_ . " ";
        }
        print "\n";
    }

    my @array = (1, 3, 5, 7, 9);

    print_array(@array);
```
需要注意的是，如果需要传入标量和数组参数时，需要把列表放在最后一个参数上。

> 传递参数为哈希

当向子程序传递哈希表时，它将复制到 `@_` 中，哈希表将被展开为键/值交替的列表:
```pl
    sub print_hash
    {
        foreach (@_) {
            print $_ . " ";
        }
        print "\n";
    }

    my %hash = ('name' => "runoob", 'age' => 3);

    print_hash(%hash);
```

> 变长参数列表

Perl 的参数机制可以很容易实现变长参数列表。
```pl
    sub max {
        my($max_so_far) = shift @_;
        foreach (@_) {
            if ($_ > $max_so_far) {
                $max_so_far = $_;
            }
        }
        $max_so_far;
    }
    
    $maxinum = &max(3, 5, 10, 4, 6);
```

[示例程序](t/03_sub_parameter.pl)


### 子程序中的私有变量

默认情况下，Perl 中所有的变量都是全局变量。

可以使用 `my` 操作符设置私有变量。通过 `my` 创建的变量，存活于声明开始的地方，直到闭合作用域的结尾。
```pl
    sub max {
        my($m, $n) = @_;
        if ($m > $n) { $m; } else { $n; }
    }
```

事实上，词法变量 `my` 可使用在任何语句块内，而不仅限于子程序的语句块。比如 if、while 或 foreach 中:
```pl
    foreach (1..10) {
        my ($square) = $_ * $_;
        print "$_ squared is $square.\n";
    }
```

### 持久性私有变量

在子程序中可以使用 `my` 操作符来创建私有变量，但每次调用这个子程序的时候，这个私有变量都会被重新定义。

使用 `state` 操作符来声明变量，我们便可以在子程序的多次调用期间保留变量之前的值，并将变量的作用域局限于子程序内部。
```pl
    sub marine {
        state $n = 0;
        $n += 1;
        print "Hello, sailor number $n\n";
    }
```

类似标量变量，其他任意类型的变量都可以被声明为 state 变量。
```pl
    sub running_sum {
        state $sum = 0;
        state @numbers;
        
        foreach my $number (@_) {
            push @numbers, $number;
            $sum += $number;
        }
        
        say "The sum of (@numbers) is $sum";
    }
    
    running_sum(5, 6);
    running_sum(1..3);
    running_sum(4);
```

