
### 前述

列表(list)指的是标量的有序集合，而数组(array)则是存储列表的变量。

在 Perl 里，这两个术语常常混用。不过更精确的说，列表指的是数据，而数组指的是变量。

数组或列表中的每个元素都是单独的标量变量，拥有独立的标量值。数组或列表中的每个元素都有相应的整数作为索引，此数字从 0 开始递增。

**因为每个元素都是独立不相关的标量值，所以列表或数组可能包含数字、字符串、undef 值或不同类型标量值的混合。**


### 数组的定义与访问

数组通过字符 `@` 表示，可以通过列表直接量定义一个数组。
```pl
    @fred = ("yabba", "dabba", "doo");  # 典型的数组定义
    @nullarray = ();                    # 空列表定义数组
    @num = (1..100);                    # 范围操作符定义数组
    @chaos = (1, 2, "hello");           # 混合数组
    @array = qw/barney betty wilma/;    # 简写形式定义
```
此外，通过 `@` 也可以引用一个数组。

数组元素可以通过 `$数组名[下标]` 完成对数组的变量的访问或赋值。
```pl
    print $fred[0];
```
如果下标超出数组的尾端，则对应的值将会是 undef 。


### 特殊的数组索引

假如对索引值超过数组尾端的元素进行赋值，数组将会根据需要自动扩大。如果在扩展过程中需要创建增补元素，那么它们的默认取值为 undef:
```pl
    $rocks[0]  = 'bedrock';
    $rocks[1]  = 'slate';
    $rocks[2]  = 'lava';
    $rocks[99] = 'schist';
```

因为 Perl 数组的可扩展性，计算出的数组长度应该是物理长度，而不是数组元素的个数。

可以通过下列方法得到数组长度:
```pl
    $end = $#rocks;
    $number_of_rocks = $end + 1;
```
`$end` 变量是数组最后一个元素的下标索引，因为数组下标从 0 开始，所以数组长度大小应在此基础上再加 1 。

当然，也可以通过下面的方式获取一个数组的长度:
```pl
    $size = @rocks;
```

[示例程序](t/01_array_size.pl)

数组索引允许为负数， -1 表示倒数第一个，-2 表示倒数第二个。


### 列表的赋值

就像标量值可被赋值给变量一样，列表值也可以被赋值给变量:
```pl
    ($fred, $barney, $dino) = ("flintstone", "rubble", undef);
    ($fred, $barney) = qw<flintstone rubble slate granite>;     # 忽略掉末尾两个元素
    ($wilma, $dino) = qw[ flintstone ];                         # dino 的值为 undef
```
左侧列表中三个变量会依次被赋予右侧列表中对应的值。

因为列表的存在，在 Perl 里互换两个变量的值相当容易:
```pl
    ($fred, $barney) = ($barney, $fred);
```

数组的复制也是以列表的语义完成的，只不过这些列表是以数组的形式存在:
```pl
    @copy = @quarry;
```


### 数组或列表的遍历

foreach 能逐项遍历列表中的值:
```pl
    foreach $rock (qw/ bedrock slate lava /) {
        print $rock . "\n";
    }
```
控制变量 `$rock` 是对列表元素的引用，而不是复制。也就是说，如果对 `$rock` 进行修改，就相当于对当前所引用的列表元素进行修改。

打印数组元素索引与该元素对应的值:
```pl
    my @rocks = qw/ bedrock slate rubble granite /;
    foreach $index (0..$#rocks) {
        print
    }
```

假如在 foreach 开头省略控制变量，Perl 会将数组元素保存到默认变量 $_ 中。

[示例程序](t/01_array_foreach.pl)


### 数组的其他操作

> push 和 pop 操作符

你可以通过索引插值的方式新增数组元素，但考虑到 Perl 数组的可扩展性，这毕竟不是一个优雅的办法。

push 操作符用于添加一个元素(或一串元素)到数组的尾端:
```pl
    @array = (5, 6);
    push(@array, 0);        # 5 6 0
    push(@array, 1..10);    # 5 6 0 1 2 3 4 5 6 7 8 9 10
    
    @others = qw(9 0 2 1 0);
    push(@array, @others);
```

与 push 对应的，pop 用来取出数组中最后一个元素并将其作为返回值返回:
```pl
    @array = (5, 6);
    $fred = pop(@array);    # $fred=6  5
```
如果数组为空，则 pop 直接返回 undef 。

通过 push 和 pop 操作，可以动态地对数组进行扩充与缩小。

[示例程序](t/01_array_push_pop.pl)

> shift 和 unshift 操作符

在操作位置上与 push 和 pop 相反，unshift 和 shift 是对数组的开头(左端)进行操作，unshift 相当于 push front, shift 相当于 pop front 。
```pl
    @array = qw/ dino fred barney /;
    $m = shift(@array);             # $m="dino"; @array=("fred", "barney");
    
    unshift(@array, "wilma");       # @array=("wilma", "fred", "barney");
```
与 pop 类似，如果数组为空， shift 直接返回 undef 。

[示例程序](t/01_array_shift_unshift.pl)

> splice 操作符

splice 用于添加或移除数组中间的某些元素。

splice 最多可接受 4 个参数，最后两个是可选的参数。第一个参数是目标数组，第二个参数是操作起始位置。

如果仅仅给出前两项参数，Perl 会把从给定位置开始一直到末尾的全部元素取出来并返回:
```pl
    @array = qw( pebbles dino fred barney betty );
    @removed = splice(@array, 2);   # @removed=(fred barney betty); @array=(pebbles dino);
```

第三个参数是要操作的元素个数，亦即长度。通过这个参数可以删掉数组中间的一个片段。
```pl
    @array = qw( pebbles dino fred barney betty );
    @removed = splice(@array, 1, 2);
```

第四个参数是要替换的列表。替换的列表的长度不一定要和拿走的元素片段一样长。
```pl
    @array = qw( pebbles dino fred barney betty );
    @removed = splice(@array, 1, 2, qw/wilma/);
```

实际上，添加元素列表并不需要预先删除某些元素，把表示长度的第三个参数设置为 0，即可不加删除地插入新列表:
```pl
    @array = qw( pebbles dino fred barney betty );
    @removed = splice(@array, 1, 0, qw/wilma/);
```
此时，返回数组为空。

[示例程序](t/01_array_splice.pl)

> reverse 操作符

读取列表(或数组)的值，并按相反的次序保存到另外一个列表返回。
```pl
    @fred = 6..10;
    @barney = reverse(@fred);   # @barney=(10, 9, 8, 7, 6);
```

非源上操作。如果需要在原目标上处理，可以这样:
```pl
    @fred = reverse(@fred);
```

[示例程序](t/01_array_reverse.pl)

> sort 操作符

简单的排序。
```pl
    @rocks = qw/ bedrock slate rubble granite /;
    @sorted = sort(@rocks);
```

非源上操作。如果需要在原目标上处理，可以这样:
```pl
    @rocks = sort(@rocks);
```

> each 操作符

食之无味，弃之也不可惜。


### 标量上下文与列表上下文

同一个表达式出现在不同的地方会有不同的意义，"不同的地方"指的就是上下文环境。

所谓上下文，指的是你如何使用表达式。或者说在不同的地方，表达式会呈现出不同的含义。

以数组的"名称"为例，在列表上下文中，它会返回元素的列表；在标量上下文中，则返回数组中的元素个数:
```pl
    @people = qw( fred barney betty );
    @sorted = sort(@people);    # 列表上下文
    $number = 42 + @people;     # 标量上下文 $number=45
```

对上下文的理解是判断 Perl 是否熟练掌握的其中一个重要因素。
